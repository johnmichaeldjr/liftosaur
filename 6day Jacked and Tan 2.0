# Week 1
## Day 1
// **T1**. Warmup up to **10 RM** for the first set, then try a 10RM, tap the first
// set and enter the 10RM weight you get. Then, do the drop sets (based on 1RM)
Squat / 1x10 75%+ (10RM), 2x6 65% (TM 70%), 1x6+ 65% (TM 70%) / update: custom() {~
  if (week >= 7 && week <= 11 && setIndex == 1) {
    weights = completedWeights[1] * (week >= 10 ? 0.9 : 0.85)
  }
~} / progress: custom() {~
  if (week == 6 || week == 12) {
    rm1 = completedWeights[1]
  }
~}

// **T2a.**
Sumo Deadlift / 4x10 50% (TM 50%)

// **T2b.** Same as in T1 - work up to 15RM, then record the weight.
// Then, do MRS (Max Rep Sets) - i.e. AMRAP, as many reps as possible
Incline Curl / 1x15 60%+ (15RM), 3x10+ 60% (MRS) / update: custom() {~ if (setIndex == 1) { weights = completedWeights[1] } ~}

// **T3.** Same as **T2b**, just 20RM
Romanian Deadlift / 1x20 50%+ (20RM), 3x10+ 50% (MRS) / update: custom() { ...Incline Curl }

// **T3.**
Hammer Curl, Dumbbell[1-6] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }

// **T3.**
Lying Leg Curl, Leverage Machine[1-6] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }

## Day 2
// ...Squat
Bench Press[1,1-12] / ...Squat / update: custom() { ...Squat } / progress: custom() { ...Squat }
// ...Sumo Deadlift
Bench Press Close Grip[2,1-5] / ...Sumo Deadlift
// ...Incline Curl
Shoulder Press[3,1-5] / ...Incline Curl
// ...Romanian Deadlift[1]
Shrug[4,1-6] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }
// ...Romanian Deadlift[1]
Pec Deck[4,1-6] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }
// ...Romanian Deadlift[1]
Triceps Pushdown[4,1-6] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }
// ...Romanian Deadlift[1]
Face Pull, Cable[4,1-6] / ...Romanian Deadlift[1]/ update: custom() { ...Incline Curl }

## Day 3
// ...Romanian Deadlift[1]
Cable Crunch[4,1-6] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }
// ...Sumo Deadlift
Lat Pulldown[3,1-5] / ...Sumo Deadlift
// ...Romanian Deadlift[1]
High Row[4,1-6] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }
// ...Romanian Deadlift[1]
Ab Wheel[4,1-6] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }
// ...Romanian Deadlift[1]
Seated Row, Leverage Machine[4,1-6] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }
// ...Romanian Deadlift[1]
Standing Calf Raise, Leverage Machine[4,1-6] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }

## Day 4
// ...Squat
Deadlift[1,1-12] / ...Squat / update: custom() { ...Squat } / progress: custom() { ...Squat }
// ...Sumo Deadlift
Front Squat[2,1-5] / ...Sumo Deadlift
// ...Incline Curl
Bicep Curl[3,1-5] / ...Incline Curl
// ...Romanian Deadlift[1]
Leg Extension[4,1-6] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }
// ...Romanian Deadlift[1]
Lunge, Bodyweight[4,1-6] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }
// ...Romanian Deadlift[1]
Bicep Curl, EZ Bar[4,1-6] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }

## Day 5
// ...Squat
Overhead Press[1,1-12] / ...Squat / update: custom() { ...Squat } / progress: custom() { ...Squat }
// ...Sumo Deadlift
Incline Bench Press[2,1-5] / ...Sumo Deadlift
// ...Incline Curl
Front Raise, Dumbbell[3,1-5] / ...Incline Curl / update: custom() { ...Incline Curl }
// ...Romanian Deadlift[1]
Triceps Extension[4,1-6] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }
// ...Romanian Deadlift[1]
Lateral Raise, Cable[4,1-6] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }
// ...Romanian Deadlift[1]
Triceps Pushdown[4,1-6] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }

## Day 6
// ...Romanian Deadlift[1]
Flat Leg Raise, Bodyweight[4,1-6] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }
// ...Sumo Deadlift
IsoLat Low Row[3,1-5] / ...Sumo Deadlift
// ...Romanian Deadlift[1]
Seated Row, Cable[4,1-6] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }
// ...Romanian Deadlift[1]
Cable Crunch[4,1-6] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }
// ...Romanian Deadlift[1]
Lat Pulldown, Leverage Machine[4,1-6] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }
// ...Romanian Deadlift[1]
Seated Leg Press, Leverage Machine[4,1-6] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }


# Week 2
## Day 1
// **T1**. Warmup up to **8 RM** for the first set, then try a 8RM, tap the first
// set and enter the 8RM weight you get. Then, do the drop sets (based on 1RM)
Squat / 1x8 80%+ (8RM), 2x6 70% (TM 75%), 1x6+ 70% (TM 75%)
Sumo Deadlift / 4x8 (TM 60%) / 60%
// **T2b.** Same as in T1 - work up to 12RM, then record the weight.
// Then, do MRS (Max Rep Sets) - i.e. AMRAP, as many reps as possible
Incline Curl / 1x12 68%+ (12RM), 3x8+ 68% (MRS)
// **T3.** Same as **T2b**, just 18RM
Romanian Deadlift / 1x18 50%+ (18RM), 3x10+ 50% (MRS)

## Day 2


## Day 3


## Day 4


## Day 5


## Day 6



# Week 3
## Day 1
// **T1**. Warmup up to **6 RM** for the first set, then try a 6RM, tap the first
// set and enter the 6RM weight you get. Then, do the drop sets (based on 1RM)
Squat / 1x6 85%+ (6RM), 2x4 75% (TM 80%), 1x4+ 75% (TM 80%)
Sumo Deadlift / 4x6 (TM 70%) / 70%
// **T2b.** Same as in T1 - work up to 10RM, then record the weight.
// Then, do MRS (Max Rep Sets) - i.e. AMRAP, as many reps as possible
Incline Curl / 1x10 73%+ (10RM), 3x6+ 73% (MRS)
// **T3.** Same as **T2b**, just 16RM
Romanian Deadlift / 1x16 55%+ (16RM), 3x8+ 55% (MRS)

## Day 2


## Day 3


## Day 4


## Day 5


## Day 6



# Week 4
## Day 1
// **T1**.
Squat / 1x4 90%+ (4RM), 2x4 75% (TM 80%), 1x4+ 75% (TM 80%)
// **T2a.**
Sumo Deadlift / 5x4 (TM 70%) / 70%
// **T2b.**
Incline Curl / 1x8 73%+ (8RM), 3x5+ 73% (MRS)
// **T3.** Same as **T2b**, just 14RM
Romanian Deadlift / 1x14 65%+ (14RM), 3x8+ 65% (MRS)

## Day 2


## Day 3


## Day 4


## Day 5


## Day 6



# Week 5
## Day 1
Squat / 1x2 95%+ (2RM), 3x2 80% (TM 85%), 1x2+ 80% (TM 85%)
Sumo Deadlift / 7x2 (TM 80%) / 80%
Incline Curl / 1x6 83%+ (6RM), 3x3+ 83% (MRS)
// **T3.** Same as **T2b**, just 12RM
Romanian Deadlift / 1x12 70%+ (12RM), 3x7+ 70% (MRS)

## Day 2


## Day 3


## Day 4


## Day 5


## Day 6



# Week 6
## Day 1
// **T1**. Testing your 1RM! This set will update 1RM of this exercise.
Squat / 1x1 (1RM) / 100%+
// **T3.** Same as **T2b**, just 10RM
Romanian Deadlift / 1x10 75%+ (10RM), 3x5+ 75% (MRS)

## Day 2
// ...Squat
Bench Press[1] / ...Squat / 100%

## Day 3


## Day 4
// ...Squat
Deadlift[1] / ...Squat / 100%

## Day 5
// ...Squat
Overhead Press[1] / ...Squat / 100%

## Day 6



# Week 7
## Day 1
// **T1**. It's start of the next cycle, where drop sets are based on your first set.
// Work up to your 6RM, after that, do the drop sets, that are 85% of your new 6RM.
Squat / 1x6 85%+ (6RM), 4x3 72.5% (6RM 85%), 1x3+ 72.5% (6RM 85%)
Sumo Deadlift / 6x5 (TM 70%) / 70%
Incline Curl / 1x12 68%+ (12RM), 3x8+ 68% (MRS)

## Day 2
Bench Press Close Grip[2,7-10] / ...Sumo Deadlift
Shoulder Press[3,7-10] / ...Incline Curl
// ...Squat
Bench Press[1,7-11] / ...Squat

## Day 3
Lat Pulldown[2,7-10] / ...Sumo Deadlift
High Row[3,7-10] / ...Incline Curl

## Day 4
Front Squat[2,7-10] / ...Sumo Deadlift
Bicep Curl[3,7-10] / ...Incline Curl
// ...Squat
Deadlift[1,7-11] / ...Squat

## Day 5
Incline Bench Press[2,7-10] / ...Sumo Deadlift
Front Raise[3,7-10] / ...Incline Curl
// ...Squat
Overhead Press[1,7-11] / ...Squat

## Day 6
IsoLat Low Row[2,7-10] / ...Sumo Deadlift
Seated Row, Cable[3,7-10] / ...Incline Curl


# Week 8
## Day 1
// **T1**.
Squat / 1x4 90%+ (4RM), 4x2 75% (4RM 85%), 1x2+ 75% (4RM 85%)
// **T2a**.
Sumo Deadlift / 5x5 (TM 75%) / 75%
// **T2b**.
Incline Curl / 1x10 73%+ (10RM), 3x6+ 73% (MRS)
// **T3.**
Romanian Deadlift / 1x18 50%+ (18RM), 3x10+ 50% (MRS)
// ...Romanian Deadlift[1]
Hammer Curl, Dumbbell[4,8-10] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }
// ...Romanian Deadlift[1]
Lying Leg Curl, Leverage Machine[4,8-10] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }

## Day 2
// ...Romanian Deadlift[1]
Shrug, Leverage Machine[4,8-10] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }
// ...Romanian Deadlift[1]
Pec Deck[4,8-10] / ...Romanian Deadlift[1]
// ...Romanian Deadlift[1]
Triceps Pushdown[4,8-10] / ...Romanian Deadlift[1]
// ...Romanian Deadlift[1]
Face Pull, Cable[4,8-10] / ...Romanian Deadlift[1]

## Day 3
// ...Romanian Deadlift[1]
Cable Crunch[4,8-10] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }
// ...Romanian Deadlift[1]
High Row[4,8-10] / ...Romanian Deadlift[1]
// ...Romanian Deadlift[1]
Ab Wheel[4,8-10] / ...Romanian Deadlift[1]
// ...Romanian Deadlift[1]
Seated Row, Leverage Machine[4,8-10] / ...Romanian Deadlift[1]
// ...Romanian Deadlift[1]
Standing Calf Raise, Leverage Machine[4,8-10] / ...Romanian Deadlift[1]

## Day 4
// ...Romanian Deadlift[1]
Leg Extension[4,8-10] / ...Romanian Deadlift[1]
// ...Romanian Deadlift[1]
Lunge, Bodyweight[4,8-10] / ...Romanian Deadlift[1]
// ...Romanian Deadlift[1]
Bicep Curl, EZ Bar[4,8-10] / ...Romanian Deadlift[1]

## Day 5
// ...Romanian Deadlift[1]
Front Raise, Dumbbell[4,8-10] / ...Romanian Deadlift[1]
// ...Romanian Deadlift[1]
Triceps Extension[4,8-10] / ...Romanian Deadlift[1]
// ...Romanian Deadlift[1]
Lateral Raise, Cable[4,8-10] / ...Romanian Deadlift[1]
// ...Romanian Deadlift[1]
Triceps Pushdown, Cable[4,8-10] / ...Romanian Deadlift[1]

## Day 6
// ...Romanian Deadlift[1]
Flat Leg Raise, Bodyweight[4,8-10] / ...Romanian Deadlift[1] / update: custom() { ...Incline Curl }
// ...Romanian Deadlift[1]
Seated Row, Cable[4,8-10] / ...Romanian Deadlift[1]
// ...Romanian Deadlift[1]
Cable Crunch[4,8-10] / ...Romanian Deadlift[1]
// ...Romanian Deadlift[1]
Lat Pulldown, Leverage Machine[4,8-10] / ...Romanian Deadlift[1]
// ...Romanian Deadlift[1]
Seated Leg Press, Leverage Machine[4,8-10] / ...Romanian Deadlift[1]


# Week 9
## Day 1
Squat / 1x2 95%+ (2RM), 4x1 80% (2RM 85%), 1x1+ 80% (2RM 85%)
Sumo Deadlift / 5x4 (TM 80%) / 80%
Incline Curl / 1x8 78%+ (8RM), 3x4+ 78% (MRS)
Romanian Deadlift / 1x16 55%+ (16RM), 3x8+ 55% (MRS)

## Day 2


## Day 3


## Day 4


## Day 5


## Day 6



# Week 10
## Day 1
Squat / 1x5 85%+ (5RM), 2x2 75% (5RM 90%), 1x2+ 75% (5RM 90%)
Sumo Deadlift / 6x3 (TM 82.5%) / 82.5%
Incline Curl / 1x6 83%+ (6RM), 3x3+ 83% (MRS)
Romanian Deadlift / 1x14 65%+ (14RM), 3x8+ 65% (MRS)

## Day 2


## Day 3


## Day 4


## Day 5


## Day 6



# Week 11
## Day 1
Squat / 1x3 92%+ (3RM), 2x1 82% (3RM 90%), 1x1+ 82% (3RM 90%)

## Day 2


## Day 3


## Day 4


## Day 5


## Day 6



# Week 12
## Day 1
// **T1**. It's week 12 - last week, it's time to test your 1RM! This set will update 1RM of this exercise.
Squat / 1x1 (1RM) / 100%+

## Day 2
// ...Squat
Bench Press[1] / ...Squat / 100%

## Day 3


## Day 4
// ...Squat
Deadlift[1] / ...Squat / 100%

## Day 5
// ...Squat
Overhead Press[1] / ...Squat / 100%

## Day 6



